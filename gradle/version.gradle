apply plugin: org.ajoberstar.gradle.git.release.base.BaseReleasePlugin

buildscript{
    repositories{
        project.buildscript.repositories.each {
            add it
        }
    }
    dependencies{
        classpath 'org.ajoberstar:gradle-git:1.1.0'
    }
}

import org.ajoberstar.grgit.Grgit
import org.ajoberstar.gradle.git.release.base.ReleaseVersion
import org.ajoberstar.gradle.git.release.base.VersionStrategy
import org.ajoberstar.gradle.git.release.semver.NearestVersionLocator
import org.ajoberstar.gradle.git.release.semver.NearestVersion

class CustomStrategy implements VersionStrategy {

    public String	getName() {'base'}

    public ReleaseVersion	infer(Project project, Grgit grgit){
        NearestVersionLocator locator=new NearestVersionLocator()
        NearestVersion nearestVersion=locator.locate(grgit)
        String branch=grgit.branch.current.name
        int distance=nearestVersion.distanceFromAny
        boolean clean = grgit.status().clean

        if(clean && distance==0)
            new ReleaseVersion(version: nearestVersion.normal)
        else
            new ReleaseVersion(version:  "${nearestVersion.normal.incrementPatchVersion()}-${branch}${distance>0?".$distance":''}+${grgit.head().id[0..5]}${clean?'':'.uncommitted'}")
    }

    public boolean	selector(Project project, Grgit grgit){
        true
    }
}


release {
    // need to specify the repository to interact with
    grgit = Grgit.open(project.file('.'))
    defaultVersionStrategy = new CustomStrategy()
}


